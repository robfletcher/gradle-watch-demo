import org.apache.tools.ant.taskdefs.condition.Os
import org.gradle.api.DefaultTask
import org.gradle.api.tasks.TaskAction

buildscript {
    repositories {
        maven { url "http://oss.jfrog.org/repo" }
        maven { url "http://dl.bintray.com/robfletcher/gradle-plugins" }
        mavenCentral()
    }
    dependencies {
        classpath "org.ratpack-framework:ratpack-gradle:0.9.0-SNAPSHOT"
        classpath "org.gradle.plugins:gradle-compass:1.0.7"
    }
}

apply plugin: "ratpack-groovy"
apply plugin: "compass"
apply plugin: "idea"

repositories {
    maven { url "http://oss.jfrog.org/repo" }
    mavenCentral()
    maven { url "http://repo.springsource.org/repo" } // for springloaded
}

dependencies {
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}

compass {
  cssDir = file("src/ratpack/public/styles")
  sassDir = file("src/main/sass")
  imagesDir = file("src/ratpack/public/images")
}

clean.dependsOn cleanCompileSass
run.dependsOn watchSass
installApp.dependsOn compileSass

task gruntCoffee(type: GruntTask) {
    gruntArgs = "coffee"
}

installApp.dependsOn gruntCoffee

task gruntWatch(type: GruntBackgroundTask) {
    gruntArgs = "watch"
}

run.dependsOn gruntWatch

task wrapper(type: Wrapper) {
    gradleVersion = "1.6"
}

class GruntTask extends DefaultTask {
    String gruntArgs = ""

    @TaskAction
    void gruntexec() {
        project.exec {
            executable = Os.isFamily(Os.FAMILY_WINDOWS) ? "grunt.cmd" : "grunt"
            args = gruntArgs.trim().split(" ") as List
        }
    }
}

class GruntBackgroundTask extends GruntTask {
    @TaskAction
    @Override
    void gruntexec() {
        Thread.start {
            super.gruntexec()
        }
    }
}
